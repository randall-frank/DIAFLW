 1  IF  PEEK (37200) <  > 134 THEN  PRINT CHR$(4);"BLOAD EDIT.CODE"
 2  LOMEM: 4 * 4096
 7  GOSUB 5000
 10 Z = 0:B = 0
 15  HGR 
 30  FOR Y = 0 TO 9
 35  FOR X = 14 TO 38 STEP 2
 40  POKE 0,X: POKE 1,Y * 16: POKE 2,Z: CALL 9 * 4096 + 256
 50 Z = Z + 1: IF Z = 128 THEN 100
 60  NEXT X,Y
 100 X = 0:Y = 0: GOSUB 10000
 110 X1 = 0:Y1 = 0: GOSUB 10010
 111 Z = 0
 170  HOME : VTAB 21: HTAB 1: PRINT "Shape=0": PRINT "Toggle=0": PRINT "Pos= X:0 Y:0"
 200  GET A$
 205  IF A$ = "B" AND B = 0 THEN B = 1:I = X:J = Y: VTAB 21: HTAB 13: PRINT "Boxing: Upper left=";I;",";J: GOTO 200
 206  IF A$ = "B" AND B = 1 THEN  GOSUB 1300
 209 M = 0
 210 Q = 1: IF  PEEK (49249) > 127 THEN Q = 7
 211  IF A$ = "W" THEN Y = Y - Q:M = 1
 220  IF A$ = "Z" THEN Y = Y + Q:M = 1
 230  IF A$ = "A" THEN X = X - Q:M = 1
 240  IF A$ = "S" THEN X = X + Q:M = 1
 241  IF M = 0 AND B = 1 THEN B = 0: VTAB 21: HTAB 13: PRINT "                          "
 242  IF A$ = "Q" THEN  TEXT : HOME : GOTO 2000
 243  IF A$ = "V" THEN 1700
 245  IF M = 0 THEN 400
 250  IF X < 0 THEN X = 0
 260  IF Y < 0 THEN Y = 0
 270  IF X > 63 THEN X = 63
 280  IF Y > 63 THEN Y = 63
 281  IF T = 1 AND M = 1 THEN  GOSUB 10020
 290  GOSUB 10000: VTAB 23: PRINT "Pos= X:";X;" Y:";Y;"  ": GOTO 200
 400  GOSUB 10010:M = 0
 401  IF A$ = "I" THEN Y1 = Y1 - 1:M = 1
 410  IF A$ = "M" THEN Y1 = Y1 + 1:M = 1
 420  IF A$ = "J" THEN X1 = X1 - 1:M = 1
 430  IF A$ = "K" THEN X1 = X1 + 1:M = 1
 435  IF M = 0 THEN  GOSUB 10010: GOTO 500
 440  IF X1 < 0 THEN X1 = 0
 450  IF Y1 < 0 THEN Y1 = 0
 460  IF Y1 > 9 THEN Y1 = 9
 470  IF X1 > 12 THEN X1 = 12
 480  IF X1 > 10 AND Y1 > 8 THEN Y1 = 8
 490  GOSUB 10010:Z = Y1 * 13 + X1
 491  VTAB 21: HTAB 1: PRINT "Shape=";Z;"  ": GOTO 200
 500  HOME : VTAB 21: HTAB 1: PRINT "Shape=";Z;"  ": PRINT "Toggle=";T: PRINT "Pos= X:";X;" Y:";Y
 510  IF A$ = " " THEN  GOSUB 10020
 550  IF A$ = "D" THEN 1000
 555  IF A$ = "T" THEN T =  NOT (T): VTAB 22: PRINT "Toggle=";T
 600  GOTO 200
 1000  PRINT : PRINT : PRINT : PRINT "Disk operations:"
 1010  PRINT "S)ave  L)oad  C)atalog"
 1020  GET A$
 1030  IF A$ = "S" THEN 1500
 1040  IF A$ = "L" THEN 1600
 1045  IF A$ = "C" THEN  POKE 49233,1: HOME : PRINT : PRINT  CHR$ (4);"CAT": GET A$: POKE 49232,1:A$ = "": GOTO 500
 1050  GOTO 200
 1100 A$ = "0123456789ABCDEF"
 1110  PRINT : PRINT "What square (#,LEVEL)";: INPUT N,L
 1115  IF N < 0 OR N > 15 OR L < 0 OR L > 3 THEN 1110
 1120 F$ = "SQUARE" +  MID$ (A$,N + 1,1) +  STR$ (L)
 1130  RETURN 
 1200  PRINT "OK? (Y/N)";: GET A$
 1201 N = 0
 1210  IF A$ = "Y" THEN N = 1
 1220  PRINT : RETURN 
 1300 I2 = I:J2 = J
 1310  IF X < I OR Y < J THEN  RETURN 
 1311  VTAB 21: HTAB 13: PRINT "Creating box, standby..."
 1320  FOR J = J2 TO Y: FOR I = I2 TO X
 1330  POKE 0,I: POKE 1,J: POKE 2,Z: CALL 9 * 4096 + 256 + 9
 1340  NEXT I,J
 1350  GOSUB 10000: RETURN 
 1500  GOSUB 1100
 1510  PRINT "Saving ";F$;"  ";
 1520  GOSUB 1200: IF N = 0 THEN 200
 1525  PRINT 
 1530  PRINT CHR$(4);"BSAVE ";F$;",A$8000,L$1100"
 1540  PRINT : PRINT : PRINT : GOTO 200
 1600  GOSUB 1100
 1610  PRINT "Loading ";F$;"  ";
 1620  GOSUB 1200: IF N = 0 THEN 200
 1625  PRINT 
 1630  PRINT CHR$(4);"BLOAD ";F$;",A$8000,L$1100"
 1635  GOSUB 10000
 1640  PRINT : PRINT : PRINT : GOTO 200
 1700  CALL 9 * 4096 + 512 + 6 * 16
 1750  GET A$: GOSUB 10000: GOTO 200
 2000 BA = 9 * 4096 + 6 * 256:A$ = ""
 2010  FOR X = 1 TO  PEEK (BA):A$ = A$ +  CHR$ ( PEEK (BA + X)): NEXT 
 2020  PRINT : PRINT  CHR$ (4);"RUN ";A$;"EDITORS"
 2030  END 
 5000  HOME : PRINT : PRINT "Insert scenario disk and press any key";: GET A$: HOME 
 5001  CALL 9 * 4096 + 7 * 256
 5002  HOME 
 5015  PRINT : PRINT CHR$(4);"BLOAD BLOCKS,A$7000"
 5020  PRINT : PRINT : PRINT : RETURN 
 10000  POKE 0,X: POKE 1,Y: CALL 9 * 4096 + 259: POKE 0,6: POKE 1,48: CALL 9 * 4096 + 268: RETURN 
 10010  POKE 0,14 + X1 * 2: POKE 1,Y1 * 16: CALL 9 * 4096 + 256 + 12: RETURN 
 10020  POKE 0,X: POKE 1,Y: POKE 2,Z: CALL 9 * 4096 + 256 + 9: GOSUB 10000: RETURN 
