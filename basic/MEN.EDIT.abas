 1  IF  PEEK (37200) <  > 134 THEN  PRINT "BLOAD EDIT.CODE"
 5  LOMEM: 4 * 4096
 7  DIM M(31,7): VTAB 22: HTAB 1
 8  GOSUB 5000
 10 Z = 0: HGR 
 11  DIM ST$(11):ST$(0) = "DEAD":ST$(1) = "NOMOVE":ST$(2) = "XFINDR":ST$(3) = "YFINDR":ST$(4) = "XYBEST":ST$(5) = "RANDOM":ST$(6) = "XY/RND"
 12 ST$(7) = "TRP/OO":ST$(8) = "TRP/ON":ST$(9) = "TRP/MO":ST$(10) = "TRP/MN":ST$(11) = "PLACER"
 30  FOR Y = 0 TO 9
 35  FOR X = 14 TO 38 STEP 2
 40  POKE 0,X: POKE 1,Y * 16: POKE 2,Z: CALL 9 * 4096 + 256
 50 Z = Z + 1: IF Z = 128 THEN 100
 60  NEXT X,Y
 100 X = 0:Y = 0: GOSUB 10000
 110 X1 = 0:Y1 = 0: GOSUB 10010
 111 Z = 0
 112 A$ = "~": GOTO 201
 200  VTAB 21: HTAB 1: PRINT "SHAPE=";Z;"       POS =";X;",";Y;"  ";: GET A$
 201 J = 0
 202  IF A$ = "Q" THEN  TEXT : HOME : GOTO 2000
 205  IF  VAL (A$) <  > 0 THEN  GOSUB 6000: GOTO 112
 209 Q = 1: IF  PEEK (49249) > 127 THEN Q = 7
 210  IF A$ = "W" THEN Y = Y - Q:J = 1
 220  IF A$ = "Z" THEN Y = Y + Q:J = 1
 230  IF A$ = "A" THEN X = X - Q:J = 1
 240  IF A$ = "S" THEN X = X + Q:J = 1
 245  IF J = 0 THEN 400
 250  IF X < 0 THEN X = 0
 260  IF Y < 0 THEN Y = 0
 270  IF X > 63 THEN X = 63
 280  IF Y > 63 THEN Y = 63
 290  GOSUB 10000: GOTO 200
 400  GOSUB 10010:J = 0
 401  IF A$ = "I" THEN Y1 = Y1 - 1:J = 1
 410  IF A$ = "M" THEN Y1 = Y1 + 1:J = 1
 420  IF A$ = "J" THEN X1 = X1 - 1:J = 1
 430  IF A$ = "K" THEN X1 = X1 + 1:J = 1
 440  IF X1 < 0 THEN X1 = 0
 450  IF Y1 < 0 THEN Y1 = 0
 460  IF Y1 > 9 THEN Y1 = 9
 470  IF X1 > 12 THEN X1 = 12
 480  IF X1 > 10 AND Y1 > 8 THEN Y1 = 8
 490  GOSUB 10010:Z = Y1 * 13 + X1
 491  IF J = 1 THEN 200
 500  HTAB 1: VTAB 22
 501  IF A$ = "O" THEN M = M - 1: IF M < 0 THEN M = 0
 502  IF A$ = "P" THEN M = M + 1: IF M > 31 THEN M = 31
 505 Q = M(M,0): IF Q > 127 THEN Q = Q - 121: IF Q = 134 THEN Q = 11
 508  IF M(M,4) > 63 OR M(M,5) > 63 THEN  PRINT M;") UNUSED.                                                                   ": GOTO 520
 510  PRINT M;") TYPE:";ST$(Q);"  POS:";M(M,4);",";M(M,5);"  SHP:";M(M,7);"    "
 511  PRINT "    WPN:";W$(M(M,1));" ";
 512  IF M(M,2) = 255 THEN  PRINT "NO AMMO         ": GOTO 520
 513  PRINT A$(M(M,2));M(M,3);"        "
 520  REM 
 525  IF A$ = "F" THEN  GOSUB 10010:X = M(M,4):Y = M(M,5):X1 = M(M,7) -  INT (M(M,7) / 13) * 13:Y1 =  INT (M(M,7) / 13): GOSUB 10010:Z = M(M,7): GOSUB 10000: GOTO 200
 530  IF A$ = " " THEN  GOSUB 7000: GOTO 112
 550  IF A$ = "D" THEN 1000
 560  IF A$ = "V" THEN 800
 600  GOTO 200
 800  HCOLOR= 0: FOR I = 0 TO 111: HPLOT 0,I TO 97,I: NEXT I
 801  HCOLOR= 3: HPLOT 15,23 TO 80,23 TO 80,88 TO 15,88 TO 15,23
 802  FOR I = 0 TO 31: HPLOT M(I,4) + 16,M(I,5) + 24: NEXT I
 803  HPLOT X + 15,Y + 24 TO X + 17,Y + 24: HPLOT X + 16,Y + 23 TO X + 16,Y + 25
 805  GET Q$: GOSUB 10000: GOTO 200
 1000  PRINT : PRINT : PRINT : PRINT "DISK OPERATIONS:"
 1010  PRINT "S)AVE  L)OAD  C)ATALOG"
 1020  GET A$
 1030  IF A$ = "S" THEN 1500
 1040  IF A$ = "L" THEN 1600
 1045  IF A$ = "C" THEN  TEXT : HOME : PRINT : PRINT  CHR$ (4);"CAT": GET A$: POKE 49232,1: GOTO 112
 1050  GOTO 200
 1100 A$ = "0123456789ABCDEF"
 1110  PRINT : PRINT "WHAT SQUARE (#,LEVEL)";: INPUT N,L
 1115  IF N < 0 OR N > 15 OR L < 0 OR L > 3 THEN 1110
 1120 F$ = "SQUARE" +  MID$ (A$,N + 1,1) +  STR$ (L)
 1130  RETURN 
 1200  PRINT "OK? (Y/N)";: GET A$
 1201 N = 0
 1210  IF A$ = "Y" THEN N = 1
 1220  PRINT : RETURN 
 1500  GOSUB 1100
 1501  GOSUB 11600
 1510  PRINT "SAVING ";F$;"  ";
 1520  GOSUB 1200: IF N = 0 THEN 200
 1525  PRINT 
 1530  PRINT "BSAVE ";F$;",A$8000,L$1100"
 1540  PRINT : PRINT : PRINT : GOTO 200
 1600  GOSUB 1100
 1610  PRINT "LOADING ";F$;"  ";
 1620  GOSUB 1200: IF N = 0 THEN 200
 1625  PRINT 
 1630  PRINT "BLOAD ";F$;",A$8000,L$1100"
 1631  GOSUB 11500
 1635  GOSUB 10000
 1640  PRINT : PRINT : PRINT : GOTO 200
 2000 BA = 9 * 4096 + 6 * 256:A$ = ""
 2010  FOR X = 1 TO  PEEK (BA):A$ = A$ +  CHR$ ( PEEK (BA + X)): NEXT 
 2020  PRINT : PRINT  CHR$ (4);"RUN ";A$;"EDITORS"
 2030  END 
 5000  HOME : PRINT : PRINT "INSERT SCENARIO DISK AND PRESS ANY KEY";: GET A$: HOME 
 5001  CALL 9 * 4096 + 7 * 256
 5002  HOME 
 5014  PRINT : PRINT "BLOAD BLOCKS,A$7000"
 5015  PRINT "BLOAD WEAPONS,A$6F00"
 5020  PRINT : PRINT : PRINT : PRINT "STANDBY.."
 5030 BA = 7 * 4096 - 256
 5100  FOR X = 0 TO 7
 5110 W$(X) = ""
 5120  FOR Y = 1 TO 10:W$(X) = W$(X) +  CHR$ ( PEEK (BA))
 5130 BA = BA + 1
 5140  NEXT Y
 5150 A$(X) = ""
 5160  FOR Y = 1 TO 9
 5170 A$(X) = A$(X) +  CHR$ ( PEEK (BA)):BA = BA + 1
 5180  NEXT Y
 5190 BA = BA + 13
 5200  NEXT X
 5201  GOSUB 11500: RETURN 
 6000  REM  MACRO CODE
 6001 V =  VAL (A$)
 6010  IF  PEEK (49249) > 127 THEN 6200
 6020 M(M,0) = D(V,0)
 6021 M(M,1) = D(V,1)
 6022 M(M,2) = D(V,2)
 6023 M(M,3) = D(V,3)
 6024 M(M,6) = D(V,6)
 6030 M(M,4) = X:M(M,5) = Y:M(M,7) = Z: RETURN 
 6200  TEXT : HOME : PRINT "DEFINING MACRO #"; VAL (V$): PRINT 
 6210  FOR QQ = 0 TO 3:D(V,QQ) = M(M,QQ): NEXT QQ:D(V,6) = M(M,6)
 6220  GOSUB 7080
 6230  FOR QQ = 0 TO 3:Q = D(V,QQ):D(V,QQ) = M(M,QQ):M(M,QQ) = Q: NEXT QQ
 6231 Q = D(V,6):D(V,6) = M(M,6):M(M,6) = Q
 6240  RETURN 
 7000  REM  WORK ON ONE
 7030 M(M,4) = X:M(M,5) = Y
 7050 M(M,7) = Z
 7075  TEXT : HOME 
 7080  PRINT "ENTER   MEANING"
 7085  PRINT "-----   ----------------------------"
 7100  PRINT "  1      NON MOVING"
 7110  PRINT "  2      XFINDER"
 7120  PRINT "  3      YFINDER"
 7130  PRINT "  4      XYBEST  (KILLER)"
 7140  PRINT "  5      RANDOM"
 7150  PRINT "  6      50% RANDOM / 50% KILLER"
 7160  PRINT "  7      TRAP / FIRE ONCE ON"
 7170  PRINT "  8      TRAP / FIRE ONCE NEAR"
 7180  PRINT "  9      TRAP / FIRE MULTIPLE ON"
 7190  PRINT "  0      TRAP / FIRE MULTIPLE NEAR"
 7191  PRINT " RET     SELF-SETUP (PLACER)"
 7192  PRINT " DEL     DEAD"
 7193  PRINT : PRINT "  ENTER STATUS CODE: ";: GET A$
 7200  REM 
 7201  IF  VAL (A$) <  > 0 AND  VAL (A$) < 7 THEN M(M,0) =  VAL (A$): GOTO 7220
 7202  IF  ASC (A$) = 13 THEN M(M,0) = 255: GOTO 7220
 7203  IF A$ = "" THEN M(M,0) = 0: GOTO 7220
 7205  IF A$ = "0" THEN M(M,0) = 131: GOTO 7220
 7206 M(M,0) =  VAL (A$) + 121
 7207  IF M(M,0) = 121 THEN 7075
 7220  HOME : INPUT "POINT VALUE OF MAN (DEFAULT 10):";A$:M(M,6) =  VAL (A$)
 7225  IF M(M,6) = 0 THEN M(M,6) = 10
 7230  IF M(M,6) < 1 OR M(M,6) > 255 THEN 7220
 7240  HOME : FOR QQ = 0 TO 7: PRINT QQ + 1;") ";W$(QQ); TAB( 20);QQ + 1;") ";A$(QQ): NEXT 
 7241  PRINT  TAB( 20);"9) NO AMMO LEFT"
 7250  PRINT : PRINT "ENTER WEAPON #:";: GET M(M,1):M(M,1) = M(M,1) - 1
 7260  IF M(M,1) > 7 OR M(M,1) < 0 THEN 7240
 7270  REM 
 7280  PRINT : PRINT "ENTER AMMO #  :";: GET M(M,2):M(M,2) = M(M,2) - 1
 7290  IF M(M,2) < 0 OR M(M,2) > 8 THEN 7270
 7295  IF M(M,2) = 8 THEN M(M,2) = 255: GOTO 7400
 7300  PRINT : PRINT "ENTER AMMOUNT OF AMMO TO BE LEFT (1-9):";: GET M(M,3)
 7310  IF M(M,3) < 1 OR M(M,3) > 9 THEN 7300
 7400  POKE  - 16297,0: POKE  - 16304,0: POKE  - 16301,0: RETURN 
 10000  POKE 0,X: POKE 1,Y: CALL 9 * 4096 + 259: POKE 0,6: POKE 1,48: CALL 9 * 4096 + 268: RETURN 
 10010  POKE 0,14 + X1 * 2: POKE 1,Y1 * 16: CALL 9 * 4096 + 256 + 12: RETURN 
 11500 BA = 9 * 4096
 11510  FOR X = 0 TO 31: FOR Y = 0 TO 7:M(X,Y) =  PEEK (BA):BA = BA + 1: NEXT Y,X: RETURN 
 11600 BA = 9 * 4096
 11610  FOR QQ = 0 TO 31: FOR QZ = 0 TO 7: POKE BA,M(QQ,QZ):BA = BA + 1: NEXT QZ,QQ: RETURN 
