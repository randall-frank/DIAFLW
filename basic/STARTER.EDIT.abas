 15  DIM W$(7),A$(7),P(7),T(7),D(7)
 16  DIM ST(5,3),Q(13)
 17  GOSUB 5000
 20  HOME 
 25  PRINT "DEATH IS A FOUR LETTER WORD STARTER EDIT"
 26  VTAB 2: HTAB 1: PRINT "----------------------------------------"
 40  PRINT : PRINT CHR$(4);"BLOAD WEAPONS,A$6F00"
 45  PRINT CHR$(4);"BLOAD STARTER,A$6E00"
 50  VTAB 20: HTAB 1: PRINT "                                        "
 90  GOSUB 1000
 100  VTAB 20: HTAB 1: PRINT "Modify: (P)osition,(D)mg,(H)ands,(N)ame"
 101  VTAB 21: HTAB 1: PRINT "        (S)ave,(Q)uit: ";
 110  GET A$
 115  VTAB 20: HTAB 1: PRINT "                                                                               "
 117  IF A$ = "Q" THEN  HOME : GOTO 3000
 120  IF A$ = "P" THEN  GOSUB 500: GOTO 100
 130  IF A$ = "S" THEN  GOSUB 2000: GOTO 100
 140  IF A$ = "D" THEN  GOSUB 300: GOTO 100
 150  IF A$ = "N" THEN  GOSUB 400: GOTO 100
 160  IF A$ = "H" THEN  GOSUB 600: GOTO 100
 200  GOTO 100
 300  VTAB 20: INPUT "Enter damage to start with:";D
 310  IF D < 1 OR D > 99 THEN 300
 320 Q(12) =  INT (D / 10):Q(13) = D - (Q(12) * 10)
 330  GOSUB 1005
 340  RETURN 
 400  VTAB 20: HTAB 2: PRINT "[";S$;"]":: VTAB 20: HTAB 3
 410  INPUT "";S$
 420  IF  LEN (S$) > 22 THEN S$ =  LEFT$ (S$,22)
 430  IF  LEN (S$) < 22 THEN S$ = S$ + " ": GOTO 430
 440  GOSUB 1005: RETURN 
 500  VTAB 20: HTAB 1: INPUT "Enter new X,Y coordinate:";Q(0),Q(1)
 510  IF Q(0) < 0 OR Q(0) > 63 OR Q(1) < 0 OR Q(1) > 63 THEN 500
 520  VTAB 21: HTAB 1: INPUT "Enter square#, level :";Q(5),Q(6)
 530  IF Q(5) < 0 OR Q(6) < 0 OR Q(5) > 15 OR Q(6) > 3 THEN 520
 550  GOSUB 1005: RETURN 
 600  REM H AND S   
 601  HOME : PRINT "Enter (1-4) for pocket # to change or   5=Right hand  AND  6=Left hand";: INPUT QQ
 602  IF QQ < 1 OR QQ > 6 THEN 601
 603 QQ = QQ - 1
 608  HOME : PRINT "Editing:"
 610  IF ST(QQ,0) = 255 THEN  PRINT "No weapon": GOTO 612
 611  PRINT W$(ST(QQ,0))
 612  IF ST(QQ,1) = 255 THEN  PRINT "None": GOTO 615
 613  PRINT A$(ST(QQ,1));ST(QQ,2)
 615  REM 
 620  PRINT : PRINT "Enter W)eapon A)mmo Q)uit:";: GET A$
 630  IF A$ = "Q" THEN  GOSUB 1005: RETURN 
 640  IF A$ = "W" THEN 800
 650  IF A$ <  > "A" THEN 608
 660  HOME : FOR X = 0 TO 7: PRINT X;") ";A$(X): NEXT 
 670  PRINT "8) None": PRINT : INPUT "Enter number to use:";QZ
 680  IF QZ < 0 OR QZ > 8 THEN 660
 690  IF QZ = 8 THEN ST(QQ,1) = 255: GOTO 608
 695  INPUT "Number of shots:";QS
 700  IF QS < 1 OR QS > 9 THEN 695
 701 ST(QQ,1) = QZ:ST(QQ,2) = QS: GOTO 608
 800  HOME : FOR X = 0 TO 7: PRINT X;") ";W$(X): NEXT 
 810  PRINT "8) None": PRINT : INPUT "Enter number to use:";QZ
 811  IF QZ < 0 OR QZ > 8 THEN 800
 812  IF QZ = 8 THEN ST(QQ,0) = 255: GOTO 608
 820 ST(QQ,0) = QZ: GOTO 608
 999  END 
 1000  GOSUB 10000
 1001  GOSUB 10200
 1005  HOME 
 1006  PRINT "DEATH IS A FOUR LETTER WORD STARTER EDIT----------------------------------------";
 1010  VTAB 4: HTAB 1: PRINT "Scenario:  ";S$
 1015  PRINT 
 1020  PRINT "Starting location:";Q(0);",";Q(1)
 1030  PRINT "        In square:";Q(5);"     Level:";Q(6)
 1040  PRINT 
 1070  PRINT "Starting damage (0=Dead):";Q(12);Q(13)
 1075  PRINT 
 1077  PRINT "Pockets contain:"
 1080  FOR X = 0 TO 5
 1085  IF X = 4 THEN  PRINT "  RT";: GOTO 1100
 1086  IF X = 5 THEN  PRINT "  LT";: GOTO 1100
 1090  PRINT "  #";X + 1;
 1100  PRINT " ";
 1110  IF ST(X,0) = 255 THEN  PRINT "No weapon  ";: GOTO 1130
 1120  PRINT W$(ST(X,0));" ";
 1130  IF ST(X,1) = 255 THEN  PRINT "No ammo  ": GOTO 1150
 1140  PRINT A$(ST(X,1));" ";ST(X,2)
 1150  IF X = 3 THEN  PRINT "Hands contain:"
 1151  NEXT X
 1160  RETURN 
 2000  HOME : PRINT "Ready to save (Y/N):";: GET A$
 2001  IF A$ <  > "Y" AND A$ <  > "N" THEN 2000
 2010  IF A$ = "N" THEN  GOSUB 1005: RETURN 
 2020  PRINT : PRINT : PRINT "Saving starter..."
 2030  GOSUB 11000
 2040  PRINT CHR$(4);"BSAVE STARTER,A$6E00,L$100"
 2050  GOSUB 1005: RETURN 
 3000 BA = 9 * 4096 + 6 * 256:A$ = ""
 3010  FOR X = 1 TO  PEEK (BA):A$ = A$ +  CHR$ ( PEEK (BA + X)): NEXT 
 3020  PRINT : PRINT  CHR$ (4);"RUN ";A$;"EDITORS"
 3030  END 
 5000  HOME : PRINT : PRINT "Insert scenario disk and press any key";: GET A$: HOME 
 5001  CALL 9 * 4096 + 7 * 256: HOME : RETURN 
 9999  END 
 10000  REM  READ
 10010 BASE = 7 * 4096 - 256
 10020  FOR X = 0 TO 7
 10030 W$(X) = ""
 10040  FOR Y = 1 TO 10:W$(X) = W$(X) +  CHR$ ( PEEK (BA))
 10050 BA = BA + 1
 10060  NEXT Y
 10065 A$(X) = ""
 10070  FOR Y = 1 TO 9
 10075 A$(X) = A$(X) +  CHR$ ( PEEK (BA)):BA = BA + 1
 10080  NEXT Y
 10090 P(X) =  PEEK (BA):BA = BA + 1
 10100 T(X) =  PEEK (BA):BA = BA + 1
 10110 D(X) =  PEEK (BA):BA = BA + 1
 10120 BA = BA + 10
 10130  NEXT X
 10140  RETURN 
 10200  REM  READ STARTER
 10210 BA = 7 * 4096 - 512
 10220  FOR X = 0 TO 13
 10230 Q(X) =  PEEK (BA):BA = BA + 1
 10240  NEXT X
 10250 BA = 7 * 4096 - 512 + 16
 10260  FOR X = 0 TO 5: FOR Y = 0 TO 3
 10270 ST(X,Y) =  PEEK (BA):BA = BA + 1
 10280  NEXT Y,X
 10290 BA = 7 * 4096 - 512 + 128
 10300 S$ = ""
 10310  FOR X = 1 TO 22:S$ = S$ +  CHR$ ( PEEK (BA)):BA = BA + 1: NEXT X
 10320  RETURN 
 11000 BA = 7 * 4096 - 512
 11010  FOR X = 0 TO 13: POKE BA,Q(X):BA = BA + 1: NEXT X
 11020 BA = 7 * 4096 - 512 + 16
 11030  FOR X = 0 TO 5: FOR Y = 0 TO 3
 11040  POKE BA,ST(X,Y):BA = BA + 1
 11050  NEXT Y,X
 11060 BA = 7 * 4096 - 512 + 128
 11070  FOR X = 1 TO 22: POKE BA, ASC ( MID$ (S$,X,1)):BA = BA + 1: NEXT X
 11080  RETURN 
