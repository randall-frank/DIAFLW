 15  DIM W$(7),A$(7),P(7),T(7),D(7)
 16  DIM PP(7)
 17  GOSUB 5000
 20  HOME 
 25  PRINT "DEATH IS A FOUR LETTER WORD WEAPONS EDIT"
 26  VTAB 2: HTAB 1: PRINT "----------------------------------------"
 40  PRINT : PRINT CHR$(4);"BLOAD WEAPONS,A$6F00"
 50  VTAB 20: HTAB 1: PRINT "                                                                            ";
 90  GOSUB 1000
 100  VTAB 21: HTAB 1: PRINT "Weapon # to edit is:";N
 110  PRINT : PRINT "Modify (N)ame,(A)mmo,(P)ower,(T)ype": PRINT " (0-7),(D)amage,(S)ave,(Q)uit: ";
 120  GET A$
 125  VTAB 23: HTAB 1: PRINT "                                                                            ";
 130  IF A$ >  = "0" AND A$ <  = "7" THEN N =  VAL (A$): GOTO 100
 140  IF A$ = "Q" THEN  HOME : GOTO 3000
 150  IF A$ = "N" THEN  GOSUB 500: GOTO 100
 160  IF A$ = "A" THEN  GOSUB 600: GOTO 100
 170  IF A$ = "P" THEN  GOSUB 700: GOTO 100
 180  IF A$ = "T" THEN  GOSUB 800: GOTO 100
 190  IF A$ = "D" THEN  GOSUB 900: GOTO 100
 200  IF A$ = "S" THEN  GOSUB 2000: GOTO 100
 210  GOTO 100
 500  HOME : VTAB 12: HTAB 2: PRINT "[";W$(N);"]"
 505  VTAB 5: HTAB 1: PRINT "Edit the weapon listed"
 510  VTAB 12: HTAB 3: INPUT "";W$(N)
 520  IF  LEN (W$(N)) > 10 THEN W$(N) =  LEFT$ (W$(N),10): GOTO 550
 530  IF  LEN (W$(N)) < 10 THEN W$(N) = W$(N) + " ": GOTO 530
 550  GOSUB 1005: RETURN 
 600  HOME : VTAB 12: HTAB 2: PRINT "[";A$(N);"]"
 605  VTAB 5: HTAB 1: PRINT "Edit the ammo listed"
 610  VTAB 12: HTAB 3: INPUT "";A$(N)
 620  IF  LEN (A$(N)) > 9 THEN A$(N) =  LEFT$ (A$(N),9): GOTO 650
 630  IF  LEN (A$(N)) < 9 THEN A$(N) = A$(N) + " ": GOTO 630
 650  GOSUB 1005: RETURN 
 700  VTAB 23: HTAB 1: INPUT "New power (% chance to kill enemy):";PP(N)
 710  IF PP(N) < 0 OR PP(N) > 99 THEN 700
 715 P(N) =  INT (PP(N) * 2.579)
 720  GOSUB 1005: RETURN 
 800  VTAB 23: HTAB 1
 803  PRINT "Enter new type value (0=single shot"
 806  INPUT " 1=explosive 2=health):";T(N)
 810  IF T(N) < 0 OR T(N) > 2 THEN 800
 820  GOSUB 1005: RETURN 
 900  VTAB 23: HTAB 1: INPUT "New damage (# damage to player):";D(N)
 910  IF D(N) < 0 OR D(N) > 255 THEN 900
 920  GOSUB 1005: RETURN 
 1000  GOSUB 10000
 1005  HOME 
 1006  PRINT "DEATH IS A FOUR LETTER WORD WEAPONS EDIT----------------------------------------";
 1010  VTAB 3: HTAB 1: PRINT "# Weapon     Ammo      Pow T Dam"
 1020  PRINT "----------------------------------------";
 1100  FOR X = 0 TO 7
 1110  VTAB X * 2 + 5: HTAB 1: PRINT X;" ";W$(X);" ";A$(X);" ";PP(X); TAB( 28);T(X); TAB( 30);D(X)
 1120  NEXT X
 1150  RETURN 
 2000  HOME : PRINT "Ready to save (Y/N):";: GET A$
 2001  IF A$ <  > "Y" AND A$ <  > "N" THEN 2000
 2010  IF A$ = "N" THEN  GOSUB 1005: RETURN 
 2020  PRINT : PRINT : PRINT "Saving weapons..."
 2030  GOSUB 11000
 2040  PRINT CHR$(4);"BSAVE WEAPONS,A$6F00,L$100"
 2050  GOSUB 1005: RETURN 
 3000 BA = 9 * 4096 + 6 * 256:A$ = ""
 3010  FOR X = 1 TO  PEEK (BA):A$ = A$ +  CHR$ ( PEEK (BA + X)): NEXT 
 3020  PRINT : PRINT  CHR$ (4);"RUN ";A$;"EDITORS"
 3030  END 
 5000  HOME : PRINT : PRINT "Insert scenario disk and press any key";: GET A$: HOME 
 5001  CALL 9 * 4096 + 7 * 256: HOME : RETURN 
 9999  END 
 10000  REM  READ
 10010 BASE = 7 * 4096 - 256
 10020  FOR X = 0 TO 7
 10030 W$(X) = ""
 10040  FOR Y = 1 TO 10:W$(X) = W$(X) +  CHR$ ( PEEK (BA))
 10050 BA = BA + 1
 10060  NEXT Y
 10065 A$(X) = ""
 10070  FOR Y = 1 TO 9
 10075 A$(X) = A$(X) +  CHR$ ( PEEK (BA)):BA = BA + 1
 10080  NEXT Y
 10090 P(X) =  PEEK (BA):BA = BA + 1
 10091 PP(X) =  INT (P(X) * .391)
 10100 T(X) =  PEEK (BA):BA = BA + 1
 10110 D(X) =  PEEK (BA):BA = BA + 1
 10120 BA = BA + 10
 10130  NEXT X
 10140  RETURN 
 11000 BA = 7 * 4096 - 256
 11010  FOR X = 0 TO 7
 11020  FOR Y = 1 TO 10
 11030  POKE BA, ASC ( MID$ (W$(X),Y,1))
 11040 BA = BA + 1
 11050  NEXT Y
 11060  FOR Y = 1 TO 9
 11070  POKE BA, ASC ( MID$ (A$(X),Y,1))
 11080 BA = BA + 1
 11090  NEXT Y
 11100  POKE BA,P(X):BA = BA + 1
 11110  POKE BA,T(X):BA = BA + 1
 11120  POKE BA,D(X):BA = BA + 1
 11140 BA = BA + 10
 11150  NEXT X
 11160  RETURN 
