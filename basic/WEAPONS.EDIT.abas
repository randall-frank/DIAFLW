 15  DIM W$(7),A$(7),P(7),T(7),D(7)
 16  DIM PP(7),LS(7),LT(7),LC(7)
 17  GOSUB 5000
 20  HOME 
 25  PRINT "Death Is A Four Letter Word Weapons Edit"
 26  VTAB 2: HTAB 1: PRINT "----------------------------------------"
 40  PRINT : PRINT CHR$(4);"BLOAD WEAPONS,A$6F00"
 45  GOSUB 10000
 50  VTAB 20: HTAB 1: PRINT "                                                                            ";
 90  GOSUB 1000
 100  VTAB 21: HTAB 1: PRINT "Weapon # to edit is:";N;" (0-7) to change."
 110  PRINT "(S)ave,(Q)uit Mod:(N)ame,(A)mmo,(T)ype"
 115  IF T(N) < 2 THEN PRINT "(P)ower,(D)amage: ";
 116  IF T(N) = 2 THEN PRINT "(H)ealth: ";
 117  IF T(N) = 3 THEN PRINT "(M)ap shapes: ";
 120  GET A$
 125  VTAB 23: HTAB 1: PRINT "                                                                            ";
 130  IF A$ >  = "0" AND A$ <  = "7" THEN N =  VAL (A$): GOTO 50
 140  IF A$ = "Q" THEN  HOME : GOTO 3000
 150  IF A$ = "N" THEN  GOSUB 500: GOTO 50
 160  IF A$ = "A" THEN  GOSUB 600: GOTO 50
 170  IF A$ = "T" THEN  GOSUB 800: GOTO 50
 175  IF T(N) <> 2 THEN 180
 177  IF A$ = "H" THEN  GOSUB 300: GOTO 50
 180  IF T(N) <> 3 THEN 185
 182  IF A$ = "M" THEN  GOSUB 400: GOTO 50
 185  IF T(N) > 1 THEN 200
 190  IF A$ = "D" THEN  GOSUB 900: GOTO 50
 195  IF A$ = "P" THEN  GOSUB 700: GOTO 50
 200  IF A$ = "S" THEN  GOSUB 2000: GOTO 50
 210  GOTO 100
 300  REM changing health
 305  GOSUB 1500 : INPUT "New health (# to heal player):";D(N)
 310  IF D(N) < 0 OR D(N) > 255 THEN 300
 320  RETURN
 400  REM changing 'key' mapping options
 410  GOSUB 1500 : INPUT "First target shape # (64-105):";LS(N)
 420  IF LS(N) < 64 OR LS(N) > 105 THEN 410
 430  GOSUB 1500 : INPUT "First replacement shape # (110-127):";LT(N)
 440  IF LT(N) < 110 OR LT(N) > 127 THEN 430
 450  GOSUB 1500 : INPUT "Number of shapes to map:";LC(N)
 460  IF LC(N) < 1 OR LC(N) > 16 THEN 450
 470  RETURN
 500  HOME : VTAB 12: HTAB 2: PRINT "[";W$(N);"]"
 505  VTAB 5: HTAB 1: PRINT "Edit the weapon listed"
 510  VTAB 12: HTAB 3: INPUT "";W$(N)
 520  IF  LEN (W$(N)) > 10 THEN W$(N) =  LEFT$ (W$(N),10): GOTO 550
 530  IF  LEN (W$(N)) < 10 THEN W$(N) = W$(N) + " ": GOTO 530
 550  RETURN 
 600  HOME : VTAB 12: HTAB 2: PRINT "[";A$(N);"]"
 605  VTAB 5: HTAB 1: PRINT "Edit the ammo listed"
 610  VTAB 12: HTAB 3: INPUT "";A$(N)
 620  IF  LEN (A$(N)) > 9 THEN A$(N) =  LEFT$ (A$(N),9): GOTO 650
 630  IF  LEN (A$(N)) < 9 THEN A$(N) = A$(N) + " ": GOTO 630
 650  RETURN 
 700  REM "Power"
 705  GOSUB 1500 : INPUT "New power (% chance to kill enemy):";PP(N)
 710  IF PP(N) < 0 OR PP(N) > 99 THEN 700
 715 P(N) =  INT (PP(N) * 2.579)
 720  RETURN
 800  GOSUB 1500
 803  PRINT "Enter new type value (0=single shot"
 806  INPUT " 1=explosive 2=health, 3=key):";T(N)
 810  IF T(N) < 0 OR T(N) > 3 THEN 800
 820  RETURN 
 900  REM "Damage"
 905  GOSUB 1500 : INPUT "New damage (# damage to player):";D(N)
 910  IF D(N) < 0 OR D(N) > 255 THEN 900
 920  RETURN
 1000  REM display state
 1010  HOME 
 1020  PRINT "Death Is A Four Letter Word Weapons Edit----------------------------------------";
 1050  VTAB 3: HTAB 1
 1080  PRINT "# Weapon     Ammo      Details"
 1090  PRINT "----------------------------------------";
 1100  FOR X = 0 TO 7
 1105  IF X = N THEN INVERSE
 1110  VTAB X * 2 + 5: HTAB 1: PRINT X;" ";W$(X);" ";A$(X);" ";
 1120  GOSUB 1300 : PRINT S$
 1135  NORMAL
 1140  NEXT X
 1150  RETURN
 1300  S$ = ""
 1310  IF T(X) = 0 THEN S$ = "sh"
 1315  IF T(X) = 1 THEN S$ = "ex"
 1320  IF T(X) = 2 THEN 1400
 1325  IF T(X) = 3 THEN 1450
 1330  S$ = S$ + " Pow:" + STR$(PP(X)) + " Dam:" + STR$(D(X))
 1340  RETURN
 1400  S$ = "heal Amount:" + STR$(D(X))
 1449  RETURN 
 1450  S$ = "Map:" + STR$(LS(X)) + "->" + STR$(LT(X)) + " #" + STR$(LC(X))
 1499  RETURN 
 1500  VTAB 21: HTAB 1
 1510  PRINT "                                        ";
 1520  PRINT "                                        ";
 1530  VTAB 22: HTAB 1
 1540  RETURN
 2000  HOME : PRINT "Ready to save (Y/N):";: GET A$
 2001  IF A$ <  > "Y" AND A$ <  > "N" THEN 2000
 2010  IF A$ = "N" THEN RETURN 
 2020  PRINT : PRINT : PRINT "Saving weapons..."
 2030  GOSUB 11000
 2040  PRINT CHR$(4);"BSAVE WEAPONS,A$6F00,L$100"
 2050  RETURN 
 3000 BA = 9 * 4096 + 6 * 256:A$ = ""
 3010  FOR X = 1 TO  PEEK (BA):A$ = A$ +  CHR$ ( PEEK (BA + X)): NEXT 
 3020  PRINT : PRINT  CHR$ (4);"RUN ";A$;"EDITORS"
 3030  END 
 5000  HOME : PRINT : PRINT "Insert scenario disk and press any key";: GET A$: HOME 
 5001  CALL 9 * 4096 + 7 * 256: HOME : RETURN 
 9999  END 
 10000  REM  READ
 10010 BASE = 7 * 4096 - 256
 10020  FOR X = 0 TO 7
 10030 W$(X) = ""
 10040  FOR Y = 1 TO 10:W$(X) = W$(X) +  CHR$ ( PEEK (BA))
 10050 BA = BA + 1
 10060  NEXT Y
 10065 A$(X) = ""
 10070  FOR Y = 1 TO 9
 10075 A$(X) = A$(X) +  CHR$ ( PEEK (BA)):BA = BA + 1
 10080  NEXT Y
 10090 P(X) =  PEEK (BA):BA = BA + 1
 10091 PP(X) =  INT (P(X) * .391)
 10100 T(X) =  PEEK (BA):BA = BA + 1
 10110 D(X) =  PEEK (BA):BA = BA + 1
 10112 LS(X) =  PEEK (BA):BA = BA + 1
 10116 LT(X) =  PEEK (BA):BA = BA + 1
 10118 LC(X) =  PEEK (BA):BA = BA + 1
 10120 BA = BA + 7
 10130  NEXT X
 10140  RETURN 
 11000 BA = 7 * 4096 - 256
 11010  FOR X = 0 TO 7
 11020  FOR Y = 1 TO 10
 11030  POKE BA, ASC ( MID$ (W$(X),Y,1))
 11040 BA = BA + 1
 11050  NEXT Y
 11060  FOR Y = 1 TO 9
 11070  POKE BA, ASC ( MID$ (A$(X),Y,1))
 11080 BA = BA + 1
 11090  NEXT Y
 11100  POKE BA,P(X):BA = BA + 1
 11110  POKE BA,T(X):BA = BA + 1
 11120  POKE BA,D(X):BA = BA + 1
 11125  POKE BA,LS(X):BA = BA + 1
 11130  POKE BA,LT(X):BA = BA + 1
 11135  POKE BA,LC(X):BA = BA + 1
 11140 BA = BA + 7
 11150  NEXT X
 11160  RETURN 
