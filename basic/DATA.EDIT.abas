 7 D$ =  CHR$ (4)
 10  DIM DA$(1000)
 13  PRINT D$;"NOMON CIO"
 20  HOME 
 25  PRINT "DEATH IS A FOUR LETTER WORD DATA EDITOR "
 26  VTAB 2: HTAB 1: PRINT "----------------------------------------"
 30  PRINT : PRINT : PRINT " THIS PROGRAM IS A SIMPLE TEXT EDITOR,"
 31  PRINT "SIMILAR TO A WORD PROCESSOR. THIS EDITOR";: PRINT "HOWEVER, CUSTOMIZES THE DATA THAT IS": PRINT "ENTERED TO FIT THE NEEDS OF THE": PRINT "D.I.A.F.L.W. SYSTEM. (NO CR'S, 40 COLS,"
 40  PRINT "AND ENDING WITH A '~')
 50  PRINT : PRINT "WARNING: IF ANY FILE NAMED 'DATA' EXISTS";
 60  PRINT "         ON THE DISK TO BE INSERTED, IT          WILL BE ERASED, THEN REPLACED           WITH THE DATA CREATED HERE."
 70  VTAB 18: PRINT "DO YOU WISH TO DO THIS NOW:(Y/N) ";: GET A$
 80  IF A$ = "N" THEN  HOME : GOTO 3000
 90  IF A$ <  > "Y" THEN  PRINT : GOTO 70
 100  GOSUB 5000
 103  PRINT : PRINT D$;"OPEN DATA": PRINT D$;"CLOSE DATA"
 104  PRINT D$;"DELETE DATA"
 105  VTAB 1
 106  HOME : VTAB 1
 110  PRINT "TEXT EDIT CMDS: QUIT, BACK, BACK#, LIST ----------------------------------------";
 111  POKE 34,2
 112 X = 1: VTAB 3: HTAB 1
 113 MAX = X
 120  HTAB 38: PRINT X;: HTAB 1
 130  HTAB 1: INPUT "";DA$(X)
 131 Y = 0
 132  IF DA$(X) = "BACK" THEN X = X - 1: GOTO 120
 133  IF DA$(X) = "QUIT" THEN 200
 134  IF  LEFT$ (DA$(X),4) = "BACK" THEN 150
 135  IF DA$(X) = "LIST" THEN MAX = X - 1: GOTO 160
 136  IF  MID$ (DA$(X),Y + 1,1) = "." THEN Y = Y + 1: GOTO 136
 137  IF Y > 0 THEN DA$(X) =  LEFT$ ("                                        ",Y) +  RIGHT$ (DA$(X), LEN (DA$(X)) - Y)
 140  IF  LEN (DA$(X)) < 40 THEN DA$(X) = DA$(X) +  RIGHT$ ("                                        ",40 -  LEN (DA$(X)))
 141 X = X + 1: IF MAX < X THEN MAX = X: GOTO 120
 150  IF  LEN (DA$(X)) = 5 THEN X =  VAL ( MID$ (DA$(X),5,1)): GOTO 120
 151  IF  LEN (DA$(X)) = 6 THEN X =  VAL ( MID$ (DA$(X),5,2)): GOTO 120
 152  GOTO 141
 160  HOME : PRINT : PRINT : PRINT "USE LINE NUMBERS? (Y/N)";: GET A$
 161  IF A$ = "N" THEN  HOME : SPEED= 200: FOR Y = 1 TO MAX: PRINT DA$(Y);: NEXT Y: GOTO 170
 162  IF A$ <  > "Y" THEN 160
 163  SPEED= 200: HOME 
 165  PRINT "1"; RIGHT$ (DA$(1),39);: FOR Y = 2 TO MAX
 167  IF Y < 10 THEN  PRINT Y; RIGHT$ (DA$(Y),39);
 168  IF Y > 9 THEN  PRINT Y; RIGHT$ (DA$(Y),38);
 169  NEXT Y
 170  SPEED= 255: PRINT : PRINT "PRESS A KEY:";: GET A$: PRINT : PRINT : GOTO 120
 200  HOME : PRINT : PRINT 
 201  PRINT "DO YOU WISH TO SAVE THE FILE OFF?(Y/N)";: GET A$: IF A$ <  > "Y" THEN  TEXT : GOTO 20
 202 MAX = MAX - 1: PRINT : PRINT : PRINT "  SAVING..."
 203  PRINT D$;"NOMON CIO"
 204  PRINT D$;"OPEN DATA": PRINT D$;"WRITE DATA"
 210  FOR Y = 1 TO MAX - 1: PRINT DA$(Y);: NEXT Y
 211  PRINT  LEFT$ (DA$(MAX),39);"~": PRINT 
 215  PRINT D$;"CLOSE DATA": TEXT : GOTO 20
 3000 BA = 9 * 4096 + 6 * 256:A$ = ""
 3010  FOR X = 1 TO  PEEK (BA):A$ = A$ +  CHR$ ( PEEK (BA + X)): NEXT 
 3020  PRINT : PRINT  CHR$ (4);"RUN ";A$;"EDITORS"
 3030  END 
 5000  HOME : PRINT : PRINT "INSERT SCENARIO DISK AND PRESS ANY KEY";: GET A$: HOME 
 5001  CALL 9 * 4096 + 7 * 256: HOME : RETURN 
