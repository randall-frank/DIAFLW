 1  IF  PEEK (37200) <  > 134 THEN  PRINT CHR$(4);"BLOAD EDIT.CODE"
 2  FOR X = 768 TO 768 + 5: READ A: POKE X,A: NEXT X
 3  DATA  1,0,4,0,7,0
 4  POKE 232,0: POKE 233,3
 6  LOMEM: 4 * 4096
 7  GOSUB 5000
 8  SCALE= 1: ROT= 0
 10 Z = 0
 11  HGR 
 30  FOR Y = 0 TO 9
 35  FOR X = 14 TO 38 STEP 2
 40  POKE 0,X: POKE 1,Y * 16: POKE 2,Z: CALL 9 * 4096 + 256
 50 Z = Z + 1: IF Z = 128 THEN 69
 60  NEXT X,Y
 69  HCOLOR= 3
 70  HPLOT 15,0 TO 15,17 TO 0,17
 75  HPLOT 16,0 TO 16,17
 76  HPLOT 12,78 TO 29,78 TO 29,97 TO 12,97 TO 12,78
 77  HPLOT 11,78 TO 11,97: HPLOT 30,78 TO 30,97
 78  HOME : VTAB 21: PRINT "Shape #0": PRINT "Color=0": PRINT "Toggle=0"
 100 X = 14:Y = 80: GOSUB 10000
 110 X1 = 0:Y1 = 0: GOSUB 10010
 200  GET A$
 201 M = 0
 202  XDRAW 1 AT X,Y
 205  IF P = 0 THEN 210
 206  HCOLOR= C
 207  HPLOT X,Y
 210  IF A$ = "W" THEN Y = Y - 1:M = 1
 220  IF A$ = "Z" THEN Y = Y + 1:M = 1
 230  IF A$ = "A" THEN X = X - 1:M = 1
 240  IF A$ = "S" THEN X = X + 1:M = 1
 242  IF A$ = "Q" THEN  TEXT : HOME : GOTO 2000
 245  IF M = 0 THEN  XDRAW 1 AT X,Y: GOTO 400
 250  IF X < 14 THEN X = 14
 260  IF Y < 80 THEN Y = 80
 270  IF X > 27 THEN X = 27
 280  IF Y > 95 THEN Y = 95
 290  GOSUB 10000: GOTO 200
 400  GOSUB 10010
 401  IF A$ = "I" THEN Y1 = Y1 - 1
 410  IF A$ = "M" THEN Y1 = Y1 + 1
 420  IF A$ = "J" THEN X1 = X1 - 1
 430  IF A$ = "K" THEN X1 = X1 + 1
 440  IF X1 < 0 THEN X1 = 0
 450  IF Y1 < 0 THEN Y1 = 0
 460  IF Y1 > 9 THEN Y1 = 9
 470  IF X1 > 12 THEN X1 = 12
 480  IF X1 > 10 AND Y1 > 8 THEN Y1 = 8
 490  GOSUB 10010:Z = Y1 * 13 + X1
 500  HOME : VTAB 21: HTAB 1: PRINT "Shape #";Z
 510  IF A$ = " " THEN  GOSUB 10000
 520  IF A$ = "U" THEN  GOSUB 10000: GOSUB 10020: GOSUB 10060: GOSUB 10000
 525  IF A$ = "P" THEN  GOSUB 10000: GOSUB 10050: GOSUB 10030: GOSUB 10000
 530  IF A$ = "T" THEN P =  NOT (P)
 540  IF A$ >  = "0" AND A$ <  = "7" THEN C =  VAL (A$)
 545  PRINT "Color=";C;"  "
 546  PRINT "Toggle=";P;"  "
 550  IF A$ = "D" THEN 1000
 560  IF A$ = "?" THEN 3000
 570  IF A$ = "#" THEN 4000
 600  GOTO 200
 1000  PRINT : PRINT : PRINT : PRINT "Disk operations:"
 1010  PRINT "S)ave  L)oad  RET)urn"
 1020  GET A$
 1021  IF A$ = "" THEN 200
 1022  PRINT : PRINT "Blocks (1) or (2):";: GET A1$
 1023 G$ = "": IF  VAL (A1$) = 2 THEN G$ = "2"
 1025  PRINT 
 1030  IF A$ = "S" THEN 1500
 1040  IF A$ = "L" THEN 1600
 1050  GOTO 200
 1100 F$ = "BLOCKS" + G$: RETURN 
 1200  PRINT "OK? (Y/N)";: GET A$
 1201 N = 0
 1210  IF A$ = "Y" THEN N = 1
 1220  PRINT : RETURN 
 1500  GOSUB 1100
 1510  PRINT "Saving ";F$;"  ";
 1520  GOSUB 1200: IF N = 0 THEN 200
 1525  PRINT 
 1530  PRINT CHR$(4);"BSAVE ";F$;",A$7000,L$1000"
 1540  PRINT : PRINT : PRINT : GOTO 200
 1600  GOSUB 1100
 1610  PRINT "Loading ";F$;"  ";
 1620  GOSUB 1200: IF N = 0 THEN 200
 1625  PRINT 
 1630  PRINT CHR$(4);"BLOAD ";F$;",A$7000"
 1631  XDRAW 1 AT X,Y
 1640  PRINT : PRINT : PRINT : GOTO 10
 2000 BA = 9 * 4096 + 6 * 256:A$ = ""
 2010  FOR X = 1 TO  PEEK (BA):A$ = A$ +  CHR$ ( PEEK (BA + X)): NEXT 
 2020  PRINT : PRINT  CHR$ (4);"RUN ";A$;"EDITORS"
 2030  END
 3000  POKE -16303,0: HOME
 3010  PRINT "Block Editor Help"
 3020  PRINT "-----------------"
 3025  PRINT
 3027  PRINT "? - Display help"
 3028  PRINT "# - Display shape definitions"
 3030  PRINT "I,J,K,M - Select shape # block"
 3040  PRINT "W,A,S,Z - Select/plot sketchpad pixel"
 3050  PRINT "SPC - Plot current sketchpad pixel"
 3060  PRINT "T - Toggle auto plotting for W,A,S,Z"
 3065  PRINT "0-7 - Change autoplot color"
 3070  PRINT "U - Fill sketchpad with selected shape"
 3080  PRINT "P - Fill selected shape from sketchpad"
 3090  PRINT "D - Disk I/O options (load/save)"
 3100  PRINT "Q - Quit the editor"
 3110  PRINT : PRINT "Press any key to continue";: GET A$
 3120  A$ = "NONE"
 3130  POKE -16304,0: GOTO 500
 4000  POKE -16303,0: HOME
 4010  PRINT "Shape definitions"
 4020  PRINT "-----------------"
 4030  PRINT
 4040  PRINT "0-3      Four facings of the player"
 4050  PRINT "4        Reserved blank space"
 4060  PRINT "5        The target shape"
 4070  PRINT "6        Indestructible terrain shape"
 4080  PRINT "7        Gunfire shape"
 4090  PRINT "8-13     Explosion shape"
 4100  PRINT "14       Up elevator/stairs shape"
 4110  PRINT "15       Down elevator/stairs shape"
 4120  PRINT "16       Dead body shape"
 4130  PRINT "17-63    Enemy/trap shapes"
 4140  PRINT "64-105   Impassable-destructible shapes"
 4150  PRINT "106-109  Rubble shapes"
 4160  PRINT "110-127  Passable terrain shapes"
 4165  PRINT : PRINT "Press any key to continue";: GET A$
 4170  A$ = "NONE"
 4180  POKE -16304,0: GOTO 500
 5000  HOME : PRINT : PRINT "Insert scenario disk and press any key";: GET A$: HOME 
 5001  CALL 9 * 4096 + 7 * 256
 5002  HOME 
 5015  PRINT : PRINT CHR$(4);"BLOAD BLOCKS,A$7000"
 5020  PRINT : PRINT : PRINT : RETURN 
 10000  XDRAW 1 AT X,Y: RETURN 
 10010  POKE 0,14 + X1 * 2: POKE 1,Y1 * 16: CALL 9 * 4096 + 256 + 12: RETURN 
 10020  POKE 0,0: POKE 1,0: POKE 2,Z: CALL 9 * 4096 + 256: RETURN 
 10030  POKE 0,0: POKE 1,0: POKE 2,Z: CALL 9 * 4096 + 256 + 15
 10031  GOSUB 10010: POKE 0,14 + X1 * 2: POKE 1,Y1 * 16: POKE 2,Z: CALL 9 * 4096 + 256: GOSUB 10010: RETURN 
 10050  FOR K = 0 TO 7: POKE 8192 + (K * 1024), PEEK (8488 + (K * 1024) + 2): POKE 8193 + (K * 1024), PEEK (8489 + (K * 1024) + 2): NEXT K
 10051  FOR K = 0 TO 7: POKE 8320 + (K * 1024), PEEK (8616 + (K * 1024) + 2): POKE 8321 + (K * 1024), PEEK (8617 + (K * 1024) + 2): NEXT K
 10052  RETURN 
 10060  FOR K = 0 TO 7: POKE 8488 + (K * 1024) + 2, PEEK (8192 + (K * 1024)): POKE 8489 + (K * 1024) + 2, PEEK (8193 + (K * 1024)): NEXT K
 10061  FOR K = 0 TO 7: POKE 8616 + (K * 1024) + 2, PEEK (8320 + (K * 1024)): POKE 8617 + (K * 1024) + 2, PEEK (8321 + (K * 1024)): NEXT K
 10062  RETURN 
